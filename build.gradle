buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.gradle:build-scan-plugin:1.4"
        classpath "com.android.tools.build:gradle:2.1.3"
        classpath "com.novoda:gradle-android-command-plugin:1.7.1"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1"
        classpath "com.vanniktech:gradle-android-apk-size-plugin:0.3.0"
        classpath "com.github.triplet.gradle:play-publisher:1.1.4"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1"
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.8"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.14.0"
        classpath "org.codehaus.groovy:gradle-groovy-android-plugin:0.3.10"
    }
}

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
}

apply from: rootProject.file("gradle/buildScan.gradle")
apply plugin: "com.android.application"
apply plugin: "groovyx.grooid.groovy-android"
apply plugin: "android-command"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.vanniktech.android.apk.size"
apply plugin: "net.ltgt.errorprone"
apply plugin: "com.github.ben-manes.versions"
apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/codeQuality.gradle")
apply from: rootProject.file("gradle/publishing.gradle")

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "burrows.apps.example.template"
        versionCode 1
        versionName "1.0"
        minSdkVersion 11
        targetSdkVersion 24
        // Espresso test runner
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // Shouldn"t the plugin be doing this for us?
    sourceSets {
        androidTest.java.srcDirs += "src/androidTest/groovy"
        main.java.srcDirs += "src/main/groovy"
        test.java.srcDirs += "src/test/groovy"
    }

    // Debug - testing and letting others use
    signingConfigs {
        debug {
            storeFile rootProject.file("./.buildscript/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile("proguard-android-optimize.txt")
            proguardFile "proguard-rules.pro"
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries !Boolean.valueOf(System.getenv("TRAVIS"))
    }

    lintOptions {
        textOutput "stdout"
        checkAllWarnings true
        warningsAsErrors true
        disable "UnusedResources"              // Unused will be removed on release
        disable "IconExpectedSize"             // Using the material icons provided from Google
        disable "GoogleAppIndexingApiWarning"  // We might want to index our app later
        disable "InvalidPackage"               // Butterknife, Okio and Realm
        disable "ResourceType"                 // Annotation binding
        disable "GradleDependency"
        disable "NewerVersionAvailable"
    }

    testOptions.unitTests {
        all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

dependencies {
    // Groovy
    compile "org.codehaus.groovy:groovy:2.4.7:grooid"

    // Android Support Library for AppCompat
    compile "com.android.support:appcompat-v7:24.1.0"

    // Espresso Instrumentation Testing
    androidTestCompile "com.android.support:support-annotations:24.1.0"
    androidTestCompile "com.android.support.test.espresso:espresso-core:2.2.2"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:2.2.2"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:2.2.2") {
        exclude module: "appcompat"
        exclude module: "support-v4"
        exclude module: "support-v7"
        exclude module: "design"
        exclude module: "support-annotations"
        exclude module: "recyclerview-v7"
    }

    // Robo Spock
    testCompile "org.robospock:robospock:1.0.0"

    // Robolectric + Support v4
    testCompile "org.robolectric:shadows-play-services:3.0"     // 3.1 breaks robospock
    testCompile "org.robolectric:shadows-support-v4:3.0"
    testCompile "org.khronos:opengl-api:gl1.1-android-2.1_r1"
}
