buildscript {
    ext.repos = {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    repositories repos

    dependencies {
        classpath "com.android.tools.build:gradle:2.1.2"
        classpath "com.novoda:gradle-android-command-plugin:1.6.0"
        classpath "com.github.triplet.gradle:play-publisher:1.1.4"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.5.5"
        classpath "com.vanniktech:gradle-android-apk-size-plugin:0.2.0"
        classpath "org.codehaus.groovy:gradle-groovy-android-plugin:0.3.10"
    }
}

plugins {
    id "com.gradle.build-scan" version "1.0"
    id "net.ltgt.errorprone" version "0.0.8"
    id "com.github.kt3k.coveralls" version "2.6.3"
    id "com.github.ben-manes.versions" version "0.13.0"
    id "build-dashboard"
    id "pmd"
    id "jacoco"
    id "findbugs"
    id "checkstyle"
}

repositories repos

apply plugin: "com.android.application"
apply plugin: "groovyx.grooid.groovy-android"
apply plugin: "android-command"
apply plugin: "com.github.triplet.play"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.vanniktech.android.apk.size"
apply from: rootProject.file("gradle/buildScan.gradle")
apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/android.gradle")
apply from: rootProject.file("gradle/codeQuality.gradle")
apply from: rootProject.file("gradle/publishing.gradle")

android {
    defaultConfig {
        applicationId "burrows.apps.example.template"
        versionCode 1
        versionName "1.0"
    }

    // Shouldn"t the plugin be doing this for us?
    sourceSets {
        androidTest {
            java {
                srcDirs += "src/androidTest/groovy"
            }
        }

        main {
            java {
                srcDirs += "src/main/groovy"
            }
        }

        test {
            java {
                srcDirs += "src/test/groovy"
            }
        }
    }

    // Debug - testing and letting others use
    signingConfigs {
        debug {
            storeFile rootProject.file("./.buildscript/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro")
            signingConfig signingConfigs.debug
        }
    }
}

configurations {
    all {
        resolutionStrategy {
            force "com.google.errorprone:error_prone_core:2.0.9"
        }
    }
}

dependencies {
    // Groovy
    compile "org.codehaus.groovy:groovy:2.4.7:grooid"

    // Android Support Library for AppCompat
    compile "com.android.support:appcompat-v7:24.0.0"

    // Espresso Instrumentation Testing
    androidTestCompile "com.android.support:support-annotations:24.0.0"
    androidTestCompile "com.android.support.test.espresso:espresso-core:2.2.2"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:2.2.2"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:2.2.2") {
        exclude module: "appcompat"
        exclude module: "support-v4"
        exclude module: "support-v7"
        exclude module: "design"
        exclude module: "support-annotations"
        exclude module: "recyclerview-v7"
    }

    // Robo Spock
    testCompile "org.robospock:robospock:1.0.0"

    // Robolectric + Support v4
    testCompile "org.robolectric:shadows-play-services:3.0"     // TODO 3.1 breaks robospock
    testCompile "org.robolectric:shadows-support-v4:3.0"
    testCompile "org.khronos:opengl-api:gl1.1-android-2.1_r1"
}
